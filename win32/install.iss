; Script generated by the Inno Setup Script Wizard.
; SEE THE DOCUMENTATION FOR DETAILS ON CREATING INNO SETUP SCRIPT FILES!

#define MyAppName "NR-Deckbuilder"
#define MyAppVersion "0.1"
#define MyAppPublisher "Geekwu.org"
#define MyAppURL "http://www.geekwu.org/"
#define MyAppExeName "NR-deckbuilder.exe"
  
#define SourcesBase "C:\perso\NR-deckbuilder"
#define GtkmmBase "C:\gtkmm"

#include "it_download.iss"

[Setup]
; NOTE: The value of AppId uniquely identifies this application.
; Do not use the same AppId value in installers for other applications.
; (To generate a new GUID, click Tools | Generate GUID inside the IDE.)
AppId={{8E2980AD-063F-416F-A786-E94115876ACD}
AppName={#MyAppName}
AppVersion={#MyAppVersion}
;AppVerName={#MyAppName} {#MyAppVersion}
AppPublisher={#MyAppPublisher}
AppPublisherURL={#MyAppURL}
AppSupportURL={#MyAppURL}
AppUpdatesURL={#MyAppURL}
DefaultDirName={pf}\{#MyAppName}
DefaultGroupName={#MyAppName}
AllowNoIcons=yes
LicenseFile={#SourcesBase}\win32\gpl-3.0.txt
OutputBaseFilename=setup
Compression=lzma
SolidCompression=yes

[Languages]
Name: "english"; MessagesFile: "compiler:Default.isl"
Name: "french"; MessagesFile: "compiler:Languages\French.isl"

[Tasks]
Name: "desktopicon"; Description: "{cm:CreateDesktopIcon}"; GroupDescription: "{cm:AdditionalIcons}"; Flags: unchecked
Name: "quicklaunchicon"; Description: "{cm:CreateQuickLaunchIcon}"; GroupDescription: "{cm:AdditionalIcons}"; Flags: unchecked; OnlyBelowVersion: 0,6.1
Name: "download"; Description: "Download base master set"

[Files]
Source: "{#SourcesBase}\src\nr_deckbuilder.ui"; DestDir: "{app}\src"; Flags: ignoreversion
Source: "{#SourcesBase}\win32\Release\NR-deckbuilder.exe"; DestDir: "{app}\bin"; Flags: ignoreversion
Source: "{#SourcesBase}\sample\test.db"; DestDir: "{app}\share"; DestName: "master.db"; Flags: onlyifdoesntexist
; NOTE: Don't use "Flags: ignoreversion" on any shared system files  
Source: "{#GtkmmBase}\bin\gdkmm-vc100-2_4.dll"; DestDir: "{app}\bin"; Components: gtkmm    
Source: "{#GtkmmBase}\bin\giomm-vc100-2_4.dll"; DestDir: "{app}\bin"; Components: gtkmm
Source: "{#GtkmmBase}\bin\glademm-vc100-2_4.dll"; DestDir: "{app}\bin"; Components: gtkmm
Source: "{#GtkmmBase}\bin\glibmm-vc100-2_4.dll"; DestDir: "{app}\bin"; Components: gtkmm
Source: "{#GtkmmBase}\bin\gtkmm-vc100-2_4.dll"; DestDir: "{app}\bin"; Components: gtkmm
Source: "{#GtkmmBase}\bin\sigc-vc100-2_0.dll"; DestDir: "{app}\bin"; Components: gtkmm
Source: "{#GtkmmBase}\bin\xml++-vc100-2_6.dll"; DestDir: "{app}\bin"; Components: gtkmm   
Source: "{#GtkmmBase}\bin\atkmm-vc100-1_6.dll"; DestDir: "{app}\bin"; Components: gtkmm   
Source: "{#GtkmmBase}\bin\cairomm-vc100-1_0.dll"; DestDir: "{app}\bin"; Components: gtkmm   
Source: "{#GtkmmBase}\bin\pangomm-vc100-1_4.dll"; DestDir: "{app}\bin"; Components: gtkmm    
Source: "{#GtkmmBase}\redist\intl.dll"; DestDir: "{app}\bin"; Components: gtk
Source: "{#GtkmmBase}\redist\freetype6.dll"; DestDir: "{app}\bin"; Components: gtk    
Source: "{#GtkmmBase}\redist\zlib1.dll"; DestDir: "{app}\bin"; Components: gtk
Source: "{#GtkmmBase}\redist\libgtk-win32-2.0-0.dll"; DestDir: "{app}\bin"; Components: gtk
Source: "{#GtkmmBase}\redist\libgdk-win32-2.0-0.dll"; DestDir: "{app}\bin"; Components: gtk
Source: "{#GtkmmBase}\redist\libgdk_pixbuf-2.0-0.dll"; DestDir: "{app}\bin"; Components: gtk
Source: "{#GtkmmBase}\redist\libpango-1.0-0.dll"; DestDir: "{app}\bin"; Components: gtk
Source: "{#GtkmmBase}\redist\libpangocairo-1.0-0.dll"; DestDir: "{app}\bin"; Components: gtk
Source: "{#GtkmmBase}\redist\libpangoft2-1.0-0.dll"; DestDir: "{app}\bin"; Components: gtk
Source: "{#GtkmmBase}\redist\libpangowin32-1.0-0.dll"; DestDir: "{app}\bin"; Components: gtk
Source: "{#GtkmmBase}\redist\libatk-1.0-0.dll"; DestDir: "{app}\bin"; Components: gtk
Source: "{#GtkmmBase}\redist\libcairo-2.dll"; DestDir: "{app}\bin"; Components: gtk
Source: "{#GtkmmBase}\redist\libgio-2.0-0.dll"; DestDir: "{app}\bin"; Components: gtk
Source: "{#GtkmmBase}\redist\libgmodule-2.0-0.dll"; DestDir: "{app}\bin"; Components: gtk
Source: "{#GtkmmBase}\redist\libgobject-2.0-0.dll"; DestDir: "{app}\bin"; Components: gtk
Source: "{#GtkmmBase}\redist\libgthread-2.0-0.dll"; DestDir: "{app}\bin"; Components: gtk
Source: "{#GtkmmBase}\redist\libglib-2.0-0.dll"; DestDir: "{app}\bin"; Components: gtk
Source: "{#GtkmmBase}\redist\libexpat-1.dll"; DestDir: "{app}\bin"; Components: gtk
Source: "{#GtkmmBase}\redist\libfontconfig-1.dll"; DestDir: "{app}\bin"; Components: gtk  
Source: "{#GtkmmBase}\redist\libpng14-14.dll"; DestDir: "{app}\bin"; Components: gtk   
Source: "{#GtkmmBase}\etc\pango\*"; DestDir: "{app}\etc\pango"; Components: gtk
Source: "{#GtkmmBase}\etc\gtk-2.0\*"; DestDir: "{app}\etc\gtk-2.0"; Components: gtk
Source: "{#GtkmmBase}\share\themes\MS-Windows\gtk-2.0\*"; DestDir: "{app}\share\themes\MS-Windows\gtk-2.0"; Components: gtk
Source: "{#GtkmmBase}\lib\gtk-2.0\2.10.0\engines\*"; DestDir: "{app}\lib\gtk-2.0\2.10.0\engines"; Components: gtk
Source: "{#GtkmmBase}\share\locale\*"; DestDir: "{app}\share\locale"; Components: gtk; Flags: recursesubdirs

;Source: "{#SourcesBase}\win32\vcredist_x86.exe"; DestDir: "{tmp}"; Components: redist

[Components]
Name: "main"; Description: "Main Files"; Types: full compact custom; Flags: fixed 
Name: "gtkmm"; Description: "GTKmm Files"; Types: full compact custom;
Name: "gtk"; Description: "GTK+ Files"; Types: full compact custom;
Name: "redist"; Description: "VC 2010 Redistribuable files"; Types: full compact custom;

[Icons]
Name: "{group}\{#MyAppName}"; Filename: "{app}\bin\{#MyAppExeName}"
Name: "{group}\{cm:UninstallProgram,{#MyAppName}}"; Filename: "{uninstallexe}"
Name: "{commondesktop}\{#MyAppName}"; Filename: "{app}\bin\{#MyAppExeName}"; Tasks: desktopicon
Name: "{userappdata}\Microsoft\Internet Explorer\Quick Launch\{#MyAppName}"; Filename: "{app}\bin\{#MyAppExeName}"; Tasks: quicklaunchicon

[Run]
Filename: "{app}\bin\{#MyAppExeName}"; Description: "{cm:LaunchProgram,{#StringChange(MyAppName, "&", "&&")}}"; Flags: nowait postinstall skipifsilent

[Code]
var   
 HasDownloadMaster: Boolean;
 HasDownloadRedist: Boolean;

procedure InitializeWizard();
begin
 ITD_init;
 ITD_DownloadAfter(wpReady);    
 HasDownloadMaster := False;
 HasDownloadRedist := False;
end;

procedure CurStepChanged(CurStep: TSetupStep);
var
  ResultCode: Integer ;
begin
 if CurStep = ssPostInstall then begin
  if HasDownloadMaster = True then begin
   filecopy(expandconstant('{app}\share\master.db'),expandconstant('{app}\share\master.db.dist'),false);
   filecopy(expandconstant('{tmp}\master.db'),expandconstant('{app}\share\master.db'),false);
  end;
  if HasDownloadRedist = True then begin
   Exec(expandconstant('{tmp}\vcredist_x86.exe'), '', '', SW_SHOW, ewWaitUntilTerminated, ResultCode);
  end;
 end;
end;

function NextButtonClick(curPageID:integer):boolean;
begin
 if curPageID = wpSelectTasks then begin
  if IsTaskSelected('download') then begin
   HasDownloadMaster := True; 
   itd_addfile('http://corrin.geekwu.org/~bastien/NR/nr-full.db',expandconstant('{tmp}\master.db'));
  end; 
 end; 
 if curPageID = wpSelectComponents then begin
  if IsComponentSelected('redist') then begin 
   HasDownloadRedist := True;
   itd_addfile('http://corrin.geekwu.org/~bastien/NR/vcredist_x86.exe',expandconstant('{tmp}\vcredist_x86.exe'));
  end; 
 end; 
 result := True;
end;

procedure CurUninstallStepChanged(CurUninstallStep: TUninstallStep);
begin
  if CurUninstallStep = usUninstall then begin
    if FileExists(expandconstant('{app}\share\master.db.dist')) then begin
      DeleteFile(expandconstant('{app}\share\master.db.dist'));
    end;
  end;
end;
